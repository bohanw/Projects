open DiskFilesHeader HolKernel

fun warn s = TextIO.output(TextIO.stdErr, s ^ "\n")

type result = (id array *
               pretype array *
               pre_vc array *
               (string * prethm) list)

%%
%name DiskFiles
%keyword IDS TYPES TERMS THEOREMS

%nonterm theoryfile of result
       | ids_section of (int,id)Binarymap.dict
       | idpair_list of (int,id)Binarymap.dict
       | idpair of id
       | types_section of (int,pretype)Binarymap.dict
       | tysize_spec
       | tmsize_spec
       | idsize_spec
       | typedecl_list of (int,pretype)Binarymap.dict
       | typedecl of pretype
       | intlist_ne of int list
       | terms_section of (int,pre_vc)Binarymap.dict
       | termdecl_list of (int,pre_vc)Binarymap.dict
       | termdecl of pre_vc
       | theorems of (string * prethm) list
       | namethm_list of (string * prethm) list
       | namethm of (string * prethm)
       | term of preterm
       | term_a of preterm
       | term_c of preterm
       | base_term of preterm
       | termlist of preterm list
       | tmid of int




%term EOF | ID of string | DOLLAR | FULLSTOP | LPAREN | RPAREN | BACKSLASH |
      IDS | TYPES | TERMS | THEOREMS | LBRACKET | RBRACKET | NUMBER of int |
      TYV | TYOP | TMV | TMC

%eop EOF
%pos int
%noshift EOF
%verbose
%pure

%start theoryfile

%%
theoryfile : ids_section types_section terms_section theorems
                (ids_section, types_section, terms_section, theorems)

ids_section : IDS idsize_spec idpair_list (idpair_list)

idsize_spec : NUMBER ()

idpair_list : (Binarymap.mkDict Int.compare)
            | idpair_list idpair
                 (Binarymap.insert(idpair_list, Binarymap.numItems idpair_list,
                                   idpair))

idpair : ID DOLLAR ID ( {Thy = ID1, Name = ID2 }  )

types_section : TYPES tysize_spec typedecl_list (typedecl_list)

tysize_spec : NUMBER ()

typedecl_list : (Binarymap.mkDict Int.compare)
              | typedecl_list typedecl
                  (Binarymap.insert(typedecl_list,
                                    Binarymap.numItems typedecl_list,
                                    typedecl))

typedecl : TYV ID (ptv ID)
         | TYOP LBRACKET intlist_ne RBRACKET
              (ptop (hd intlist_ne, tl intlist_ne))

intlist_ne : NUMBER ([NUMBER])
           | NUMBER intlist_ne (NUMBER::intlist_ne)

terms_section : TERMS tmsize_spec termdecl_list (termdecl_list)

tmsize_spec : NUMBER ()

termdecl_list : (Binarymap.mkDict Int.compare)
              | termdecl_list termdecl
                   (Binarymap.insert(termdecl_list,
                                     Binarymap.numItems termdecl_list,
                                     termdecl))

termdecl : TMV LBRACKET ID NUMBER RBRACKET (ptm_v(ID, NUMBER))
         | TMC LBRACKET NUMBER NUMBER RBRACKET
             (ptm_c(NUMBER1, NUMBER2))

theorems : THEOREMS namethm_list (namethm_list)

namethm_list : ([]) | namethm namethm_list (namethm :: namethm_list)

namethm : ID termlist ((ID, (tl termlist, hd termlist)))

termlist : term ([term])
         | term termlist (term :: termlist)

term : LPAREN term_a RPAREN (term_a)

term_a :
   BACKSLASH tmid FULLSTOP term_a (abs(tmid, term_a))
 | term_c (term_c)

term_c : term_c base_term (app(term_c, base_term))
       | base_term (base_term)

base_term : LPAREN term_a RPAREN (term_a)
          | tmid (atom tmid)

tmid :  NUMBER (NUMBER)

