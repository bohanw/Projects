(*---------------------------------------------------------------------------
     Various iterative primitive recursions.
     We start with Ackermann's function.
 ---------------------------------------------------------------------------*)

(* Based on iteration alone *)
val Ackermann_Existence = Q.prove
(`?f. (!n. f 0 n = n + 1) /\ 
      (!m. f (m + 1) 0 = f m 1) /\
      (!m n. f (m + 1) (n + 1) = f m (f (m + 1) n))`,
 Q.EXISTS_TAC `SIMP_REC SUC (\f x. SIMP_REC (f 1) f x)`
   THEN RW_TAC arith_ss [prim_recTheory.SIMP_REC_THM, 
                         GSYM arithmeticTheory.ADD1]);


(* Using WFREC *)

val Ack_def = 
 Define
  `(Ack (0,n) =  n+1) /\
   (Ack (SUC m,0) = Ack (m, 1)) /\
   (Ack (SUC m, SUC n) = Ack (m, Ack (SUC m, n)))`;

val Ack_ind = fetch "-" "Ack_ind";

val Ack_positive = Q.prove
(`!x y. 0 < Ack(x,y)`,
  recInduct Ack_ind 
    THEN ONCE_REWRITE_TAC[Ack_def] 
    THEN PROVE_TAC [DECIDE (Term`0 < x + 1`)]);


val Ack_grows_faster_than_plus = Q.prove
(`!x y. x+y < Ack(x,y)`,
 recInduct Ack_ind
  THEN ONCE_REWRITE_TAC[Ack_def] 
  THEN RW_TAC arith_ss []);


(*---------------------------------------------------------------------------*
 * Sudan's function, which is similar to Ackermann's function. Apparently,   *
 * both Sudan and Ackermann were students of Hilbert and came up with their  *
 * functions about the same time, but Hilbert preferred Ackermann's.         *
 *---------------------------------------------------------------------------*)

val Sudan_def = 
 Define
   `(Sudan 0 (x,y)           = x+y)
 /\ (Sudan (SUC n) (x,0)     = x)
 /\ (Sudan (SUC n) (x,SUC y) = Sudan n (Sudan (SUC n) (x,y), 
                                        Sudan (SUC n) (x,y) + SUC y))`;

(*---------------------------------------------------------------------------
       A Harvey Friedman function (from May 1999 FOM posting).
 ---------------------------------------------------------------------------*)

val V_def = 
 Define
   `(V (SUC 0, n, m)                = n) 
/\  (V (SUC(SUC k), n, SUC 0)       = V (SUC k, SUC n, SUC n))
/\  (V (SUC(SUC k), n, SUC (SUC m)) = V (SUC k, V(SUC(SUC k),n,SUC m)+1,
                                                V(SUC(SUC k),n,SUC m)+1))`;
